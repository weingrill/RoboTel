<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.6">
  <POU Name="AzimuthElevationControl" Id="{8e08648a-b748-4448-9dcc-aa13dfcf6a06}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM AzimuthElevationControl
VAR_INPUT
	// decrease azimuth
	left:		BOOL;
	// increase azimuth
	right: 		BOOL;
	// reset the axis
	Reset: 		BOOL;
	// enable the axis and its position control loop
	Enable:		BOOL;
	// Move azimuth to given position
	MoveAxis:	BOOL;
	// Home the axis
	HomeAxis:	BOOL;
	// enable tracking
	Tracking:	BOOL;
	// position to move the axis to
	position: 	LREAL;
END_VAR
VAR_OUTPUT
	// error state of the axis
	error: 			BOOL;
	// error ID of the axis
	ErrorID:		UDINT;
	// actual_position of the axis
	act_position:	LREAL;
	// is the axis calibrated
	calibrated:		BOOL;
	// drive is ready
	ready:			BOOL;
END_VAR
VAR
	AzimuthAxis:		FB_Axis2;
	AzimuthCalibration: MC_SetPosition;
	AzimuthAxisEvent: 	FB_EventLog;
	// delay until commutator angle is measured
	readydelay:			TON;
	HomeDelay:				FB_TONTP;
END_VAR
VAR CONSTANT
	// reference position for homing
	homing_position:		LREAL := 439.0; // was 442 - -21.21 = 463.21; 463 + 24 = 439
	// current position of the calibration cam. Apply an offset here if necessary
	calibration_position:	LREAL := 180.0 - 24.0;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* Azimuth Axis control section
Power must be on and brake must be open for the axis to operate
*)

(*
Enable := TelescopeControl.power OR PendantControl.Azimuth_enable;
*)
IF Enable THEN
	ElevationControl.enable := TRUE;
	// HydraulicsControl.open_brake := TRUE;
END_IF

// generate the ready signal after enabling the drive
readydelay(IN :=  AzimuthAxis.Enable, PT := T#6000MS, Q=> ready);

// limit the input position angle
position := LIMIT(0.0, position, 450.0);

// conduct the calibrated signal
calibrated := AzimuthAxis.Calibrated;

IF AzimuthAxis.Calibrated OR Error THEN
	HomeAxis := FALSE;
END_IF
	
IF HomeAxis THEN
	position := homing_position;
END_IF

// move to calibration_position after homing
HomeDelay(IN := Calibrated, PT1 := T#5S, PT2 := T#100MS);

IF HomeDelay.Q THEN
	position := calibration_position;
	MoveAxis := TRUE;
END_IF

// perform a recalibration, if we pass the reference mark on the encoder
AzimuthCalibration(
	Axis := GVL_Telescope.AzimuthAxisRef,
	Execute := GVL_Telescope.Azimuth_Probe>0 AND NOT HomeAxis,
	Position := calibration_position); // TODO: Options := ST_SetPositionOptions.ClearPositionLag

// deactivate limit switches on homing
MC_SetAcceptBlockedDriveSignal(
	Axis := GVL_Telescope.AzimuthAxisRef, 
	Enable := HomeAxis);

AzimuthAxis(	
	Enable := 			enable AND HydraulicsControl.brake_open,
	Reset := 			reset,
	MoveAxis :=			MoveAxis,
	HomeAxis := 		HomeAxis,
	Tracking :=			Tracking,
	Position := 		position, 
	Enable_Positive := 	NOT GVL_Telescope.azimut_limit_switch AND (GVL_Telescope.Azimuth_inputs.1 OR HomeAxis),
	Enable_Negative := 	NOT GVL_Telescope.azimut_limit_switch AND GVL_Telescope.Azimuth_inputs.0,
	Jog_Forward :=		right,
	Jog_Backwards := 	left,
	bCalibrationCam :=	NOT GVL_Telescope.Azimuth_inputs.1,
	ActualPosition => 	act_position,
	Error => 			error,
	ErrorID => 			ErrorID,
	AxisRef :=			GVL_Telescope.AzimuthAxisRef);

IF Error THEN
	HydraulicsControl.close_brake := TRUE;
END_IF
	
IF AzimuthAxis.MoveDone THEN
	MoveAxis := FALSE;
END_IF

IF AzimuthAxis.HomeDone THEN
	HomeAxis := FALSE;
END_IF
	
IF AzimuthAxis.ResetDone THEN
	reset := FALSE;
END_IF

AzimuthAxisEvent(	
	Trigger := 		Error, 
	Level := 		ADSLOG_MSGTYPE_ERROR,
	FormatString :=	'Azimuth Axis Error: %s',
	OnMessage := 	NCError_TO_STRING(ErrorID));
				]]></ST>
    </Implementation>
    <LineIds Name="AzimuthElevationControl">
      <LineId Id="172" Count="0" />
      <LineId Id="174" Count="0" />
      <LineId Id="173" Count="0" />
      <LineId Id="251" Count="0" />
      <LineId Id="179" Count="1" />
      <LineId Id="128" Count="0" />
      <LineId Id="114" Count="0" />
      <LineId Id="229" Count="0" />
      <LineId Id="152" Count="1" />
      <LineId Id="340" Count="1" />
      <LineId Id="284" Count="0" />
      <LineId Id="345" Count="0" />
      <LineId Id="344" Count="0" />
      <LineId Id="243" Count="0" />
      <LineId Id="346" Count="0" />
      <LineId Id="333" Count="0" />
      <LineId Id="285" Count="0" />
      <LineId Id="334" Count="0" />
      <LineId Id="244" Count="6" />
      <LineId Id="400" Count="0" />
      <LineId Id="393" Count="6" />
      <LineId Id="347" Count="0" />
      <LineId Id="287" Count="4" />
      <LineId Id="238" Count="4" />
      <LineId Id="115" Count="0" />
      <LineId Id="33" Count="0" />
      <LineId Id="108" Count="0" />
      <LineId Id="39" Count="0" />
      <LineId Id="126" Count="0" />
      <LineId Id="252" Count="0" />
      <LineId Id="337" Count="0" />
      <LineId Id="41" Count="0" />
      <LineId Id="104" Count="3" />
      <LineId Id="237" Count="0" />
      <LineId Id="38" Count="0" />
      <LineId Id="40" Count="0" />
      <LineId Id="85" Count="0" />
      <LineId Id="35" Count="0" />
      <LineId Id="175" Count="0" />
      <LineId Id="156" Count="0" />
      <LineId Id="177" Count="1" />
      <LineId Id="176" Count="0" />
      <LineId Id="158" Count="10" />
      <LineId Id="157" Count="0" />
      <LineId Id="79" Count="0" />
      <LineId Id="109" Count="0" />
      <LineId Id="80" Count="2" />
      <LineId Id="5" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>