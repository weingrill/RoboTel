<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.9">
  <POU Name="HydraulicsControl" Id="{6bf4055d-2300-4039-bb16-5b9f94622e06}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM HydraulicsControl
VAR_INPUT
	// start the hydraulics
	start:		BOOL := FALSE;
	// stop the hydraulics
	stop: 		BOOL;
	// command to open the brake
	open_brake:	BOOL := FALSE;
	// command to open the brake
	close_brake: BOOL;
END_VAR
VAR_OUTPUT
	// status of the brake: is the brake open?
	brake_open:			BOOL;
	// is the brake closed?
	brake_closed:		BOOL;
	// pump is running, but the pressure remains low
	pressure_failure:	BOOL;
	// suction was started, but it is not running
	suction_failure:	BOOL;
	// oil is low
	oil_low:			BOOL;
	// oil is high (unused?)
	oil_high:			BOOL;
	// oil pressure is 25 bars
	oil_pressure_OK:	BOOL;
	// oil is on reserve
	oil_warning:		BOOL;
	// oil filter is dirty
	oil_filter_dirty:	BOOL;
	// oil is below 10°C
	oil_cold:			BOOL;
	// oil is above 50°C
	oil_hot:			BOOL;
	// main pump is running
	pump_running:		BOOL;
	// suction pump is running
	suction_running :	BOOL := FALSE;
	// error condition
	error: BOOL;
END_VAR
VAR
	// monitors the pressure of the pump is running
	pressure_watchdog:	TON;
	// monitors the activation of the suction pump
	suction_watchdog: TON;
	// the suction pump is activated for at least 28s when maximum is reached
	suction_pump_timer: TP;
	// represents the state of the main pump
	oil_pump_state:		RS;
	// represents the state of the brake
	brake_state: RS;
	// wait for the brake to open after pressure has been established
	brake_open_delay:	TON;
	// report brake opend after a short delay
	brake_opened_delay:	TON;
	HydraulicsLog,
	PumpLog,
	ReserveLog,
	oil_low_log,
	FilterLog,
	OverTemperatureLog,
	UnderTemperatureLog,
	SuctionLog,
	BreakClearingWarning,
	BreakHydraulicsWarning: FB_EventLog;
	
	// measures the operating cycle of the suction pump
	suction_timer : FB_Timer;
	// MQTT Message
	sPayloadPub: STRING(255);
	rftrig:		FB_RFTRIG;
	StatusWord, LastStatus:	UDINT;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* 
Main control Logic for the Hydraulics system
* power on delay for main pump and suction pump
* start main pump if main control is ready (button pressed or remote)
* start suction pump if maximum in pan is reached
* suction pump runs for 60 seconds
* stop main pump if oil is low
* stop main pump if pressure fails for 10 seconds
*)
oil_low := NOT GVL_Hydraulics.oil_low;
oil_high := NOT GVL_Hydraulics.oil_high;
oil_warning := GVL_Hydraulics.oil_reserve;
// Michi Email 22.10.2020 True = OK
oil_pressure_OK := GVL_Hydraulics.oil_pressure_OK;
oil_filter_dirty := NOT GVL_Hydraulics.oilfilter_dirty;
oil_cold := NOT GVL_Hydraulics.oil_undertemperature;
oil_hot := GVL_Hydraulics.oil_overtemperature;
pump_running := GVL_Hydraulics.pump_running;
suction_running := GVL_Hydraulics.suction_running;

IF stop THEN
	start := FALSE;
END_IF

oil_pump_state(SET := start,
				RESET1 := stop OR
						pressure_failure OR
						suction_failure OR 
						oil_low OR
						NOT MAIN.phase_available);

GVL_Hydraulics.oil_pump_enable := oil_pump_state.Q1;

IF NOT pump_running THEN
	stop := FALSE;
END_IF
(* This section controls the hydraulic brake responsible for azimuth and elevation. 
Especially the elevation is imbalanced if not in the position control loop. 
The brake must only be opened, if the elevation drive is activated concurrently.
*)
// reset the open_brake command if the close_brake is active


// wait for pressure to build up (hack for oil_pressure_ok)
brake_open_delay(IN := pump_running AND oil_pressure_OK,
				PT := T#3S);
//manually or remotly open brake. Close brake on limit switch. Wait n seconds after pump is on
//hold the brake, if we reach the az-limit	
// close the brake if the pump is not running	

IF close_brake OR NOT pump_running THEN 
	open_brake := FALSE;
END_IF
		
brake_state(SET := brake_open_delay.Q AND open_brake AND MAIN.brake_clearing,
			RESET1 := GVL_Telescope.azimut_limit_switch 
					  OR close_brake 
					  OR NOT pump_running
					  OR NOT MAIN.brake_clearing,
			Q1 => GVL_Telescope.brake_azimuth_eve);

brake_opened_delay(	IN :=brake_state.Q1, 
					PT := T#1000MS,
					Q => brake_open);

brake_closed := NOT brake_open;
IF brake_closed THEN
	close_brake := FALSE;
END_IF

error := pressure_failure OR suction_failure OR oil_low;
(* This is the end of the brake control section *)

// measure suction pump cycle
suction_timer(	bStart := suction_running AND NOT suction_pump_timer.Q, 
				bStop := GVL_Hydraulics.oilpan_maximum);  

suction_pump_timer(IN := MAIN.phase_available AND 
						GVL_Hydraulics.oilpan_maximum,
					PT := T#28S,
					Q => GVL_Hydraulics.oil_suction_enable);
					
suction_watchdog(IN := GVL_Hydraulics.oil_suction_enable AND NOT suction_running,
					PT := T#10S,
					Q => suction_failure);
					
pressure_watchdog(IN := GVL_Hydraulics.oil_pump_enable AND 
						NOT oil_pressure_OK,
					PT := T#15S,
				  Q => pressure_failure);

(*
oil_low := NOT GVL_Hydraulics.oil_low;
oil_high := NOT GVL_Hydraulics.oil_high;
oil_warning := GVL_Hydraulics.oil_reserve;
// Michi Email 22.10.2020 True = OK
oil_pressure_OK := GVL_Hydraulics.oil_pressure_OK;
oil_filter_dirty := NOT GVL_Hydraulics.oilfilter_dirty;
oil_cold := NOT GVL_Hydraulics.oil_undertemperature;
oil_hot := GVL_Hydraulics.oil_overtemperature;
pump_running := GVL_Hydraulics.pump_running;
suction_running := GVL_Hydraulics.suction_running;

*)
StatusWord.0 := oil_low;
StatusWord.1 := oil_high;
StatusWord.2 := oil_warning;
StatusWord.3 := oil_pressure_OK;
StatusWord.4 := oil_filter_dirty;
StatusWord.5 := oil_cold;
StatusWord.6 := oil_hot;
StatusWord.7 := brake_open;
StatusWord.8 := GVL_Main.brake_clearing;
StatusWord.9 := pump_running;
StatusWord.10 := suction_running;

IF StatusWord<>LastStatus THEN // publish new payload every second
	LastStatus := StatusWord;
	sPayloadPub := 'electronics,location=base,host=CX-4E6032,module=HydraulicsControl ';
	sPayloadPub := CONCAT(sPayloadPub, 'OilLow=');
	sPayloadPub := CONCAT(sPayloadPub, BOOL_TO_STRING(oil_low));
	sPayloadPub := CONCAT(sPayloadPub, ',OilHigh=');
	sPayloadPub := CONCAT(sPayloadPub, BOOL_TO_STRING(oil_high));
	sPayloadPub := CONCAT(sPayloadPub, ',OilReserve=');
	sPayloadPub := CONCAT(sPayloadPub, BOOL_TO_STRING(oil_warning));
	sPayloadPub := CONCAT(sPayloadPub, ',OilPressureOK=');
	sPayloadPub := CONCAT(sPayloadPub, BOOL_TO_STRING(oil_pressure_OK));
	sPayloadPub := CONCAT(sPayloadPub, ',OilFilterDirty=');
	sPayloadPub := CONCAT(sPayloadPub, BOOL_TO_STRING(oil_filter_dirty));
	sPayloadPub := CONCAT(sPayloadPub, ',OilCold=');
	sPayloadPub := CONCAT(sPayloadPub, BOOL_TO_STRING(oil_cold));
	sPayloadPub := CONCAT(sPayloadPub, ',OilHot=');
	sPayloadPub := CONCAT(sPayloadPub, BOOL_TO_STRING(oil_Hot));
	
	MQTTCommunication.fbMqttClient.Publish(	
					sTopic:= 'RoboTel/Telemetry', 
					pPayload:= ADR(sPayloadPub), 
					nPayloadSize:= LEN2(ADR(sPayloadPub)),
					eQoS:= TcIotMqttQos.AtMostOnceDelivery,
					bRetain:= FALSE, 
					bQueue:= FALSE );

	sPayloadPub := 'electronics,location=base,host=CX-4E6032,module=HydraulicsControl ';
	sPayloadPub := CONCAT(sPayloadPub, 'BrakeOpen=');
	sPayloadPub := CONCAT(sPayloadPub, BOOL_TO_STRING(brake_open));
	sPayloadPub := CONCAT(sPayloadPub, ',BrakeClearing=');
	sPayloadPub := CONCAT(sPayloadPub, BOOL_TO_STRING(GVL_Main.brake_clearing));
	sPayloadPub := CONCAT(sPayloadPub, ',PumpRunning=');
	sPayloadPub := CONCAT(sPayloadPub, BOOL_TO_STRING(pump_running));
	sPayloadPub := CONCAT(sPayloadPub, ',SuctionRunning=');
	sPayloadPub := CONCAT(sPayloadPub, BOOL_TO_STRING(suction_running));
	
	MQTTCommunication.fbMqttClient.Publish(	
					sTopic:= 'RoboTel/Telemetry', 
					pPayload:= ADR(sPayloadPub), 
					nPayloadSize:= LEN2(ADR(sPayloadPub)),
					eQoS:= TcIotMqttQos.AtMostOnceDelivery,
					bRetain:= FALSE, 
					bQueue:= FALSE );
	
END_IF


				  				  
HydraulicsLog(Trigger := pressure_failure,
				Level := ADSLOG_MSGTYPE_ERROR,
				FormatString := 'oil pressure: %s',
				OnMessage := 'FAILURE',
				OffMessage := 'OK',
				OffLevel := ADSLOG_MSGTYPE_HINT);

PumpLog(Trigger := pump_running,
				Level := ADSLOG_MSGTYPE_HINT,
				FormatString := 'Main hydraulics pump: %s',
				OnMessage := 'START',
				OffMessage := 'STOP',
				OffLevel := ADSLOG_MSGTYPE_HINT);
	
ReserveLog(Trigger := oil_warning,
			Level := ADSLOG_MSGTYPE_WARN,
			FormatString := 'oil reserve: %s',
			OnMessage := 'LOW',
			OffMessage := 'OK',
			OffLevel := ADSLOG_MSGTYPE_HINT);

oil_low_log(Trigger := oil_low,
				Level := ADSLOG_MSGTYPE_ERROR,
				FormatString := 'oil level: %s',
				OnMessage := 'LOW',
				OffMessage := 'OK',
				OffLevel := ADSLOG_MSGTYPE_HINT);

FilterLog(Trigger := oil_filter_dirty,
				Level := ADSLOG_MSGTYPE_WARN,
				FormatString := 'Hydraulics Filter is %s',
				OnMessage := 'dirty',
				OffMessage := 'clean',
				OffLevel := ADSLOG_MSGTYPE_HINT);

OverTemperatureLog(Trigger := oil_hot,
				Level := ADSLOG_MSGTYPE_WARN,
				FormatString := 'Hydraulics Temperature %s',
				OnMessage := '> 50C',
				OffMessage := '< 50C',
				OffLevel := ADSLOG_MSGTYPE_HINT);
UnderTemperatureLog(Trigger := oil_cold,
				Level := ADSLOG_MSGTYPE_WARN,
				FormatString := 'Hydraulics Temperature %s',
				OnMessage := '< 10C',
				OffMessage := '> 10C',
				OffLevel := ADSLOG_MSGTYPE_HINT);

SuctionLog(Trigger := suction_failure,
				Level := ADSLOG_MSGTYPE_ERROR,
				FormatString := 'suction pump: %s',
				OnMessage := 'FAILURE',
				OffMessage := 'OK',
				OffLevel := ADSLOG_MSGTYPE_HINT);

BreakClearingWarning(Trigger := NOT Main.brake_clearing AND open_brake,
				Level := ADSLOG_MSGTYPE_WARN,
				FormatString := '%s',
				OnMessage := 'Cannot open brake without brake clearing');

BreakHydraulicsWarning(Trigger := NOT start AND open_brake,
				Level := ADSLOG_MSGTYPE_WARN,
				FormatString := '%s',
				OnMessage := 'Cannot open brake without running hydraulics');
]]></ST>
    </Implementation>
    <LineIds Name="HydraulicsControl">
      <LineId Id="308" Count="11" />
      <LineId Id="454" Count="0" />
      <LineId Id="321" Count="5" />
      <LineId Id="702" Count="3" />
      <LineId Id="333" Count="3" />
      <LineId Id="493" Count="0" />
      <LineId Id="337" Count="1" />
      <LineId Id="786" Count="0" />
      <LineId Id="340" Count="0" />
      <LineId Id="788" Count="0" />
      <LineId Id="787" Count="0" />
      <LineId Id="789" Count="1" />
      <LineId Id="549" Count="0" />
      <LineId Id="551" Count="1" />
      <LineId Id="550" Count="0" />
      <LineId Id="621" Count="0" />
      <LineId Id="620" Count="0" />
      <LineId Id="622" Count="0" />
      <LineId Id="342" Count="4" />
      <LineId Id="416" Count="0" />
      <LineId Id="691" Count="0" />
      <LineId Id="689" Count="0" />
      <LineId Id="692" Count="0" />
      <LineId Id="690" Count="0" />
      <LineId Id="693" Count="0" />
      <LineId Id="347" Count="1" />
      <LineId Id="455" Count="1" />
      <LineId Id="694" Count="0" />
      <LineId Id="349" Count="0" />
      <LineId Id="559" Count="0" />
      <LineId Id="558" Count="0" />
      <LineId Id="560" Count="1" />
      <LineId Id="625" Count="0" />
      <LineId Id="554" Count="0" />
      <LineId Id="629" Count="0" />
      <LineId Id="695" Count="1" />
      <LineId Id="859" Count="0" />
      <LineId Id="626" Count="0" />
      <LineId Id="555" Count="0" />
      <LineId Id="547" Count="0" />
      <LineId Id="350" Count="1" />
      <LineId Id="494" Count="0" />
      <LineId Id="352" Count="4" />
      <LineId Id="936" Count="0" />
      <LineId Id="357" Count="3" />
      <LineId Id="362" Count="3" />
      <LineId Id="1045" Count="0" />
      <LineId Id="1013" Count="0" />
      <LineId Id="1049" Count="9" />
      <LineId Id="1048" Count="0" />
      <LineId Id="1047" Count="0" />
      <LineId Id="1046" Count="0" />
      <LineId Id="1065" Count="1" />
      <LineId Id="1068" Count="5" />
      <LineId Id="1067" Count="0" />
      <LineId Id="1075" Count="0" />
      <LineId Id="1135" Count="0" />
      <LineId Id="1017" Count="0" />
      <LineId Id="1019" Count="3" />
      <LineId Id="1059" Count="5" />
      <LineId Id="1076" Count="5" />
      <LineId Id="1031" Count="7" />
      <LineId Id="1082" Count="9" />
      <LineId Id="1098" Count="7" />
      <LineId Id="1039" Count="1" />
      <LineId Id="1014" Count="1" />
      <LineId Id="366" Count="0" />
      <LineId Id="369" Count="5" />
      <LineId Id="937" Count="0" />
      <LineId Id="930" Count="5" />
      <LineId Id="929" Count="0" />
      <LineId Id="375" Count="5" />
      <LineId Id="938" Count="0" />
      <LineId Id="381" Count="5" />
      <LineId Id="939" Count="0" />
      <LineId Id="387" Count="5" />
      <LineId Id="940" Count="0" />
      <LineId Id="393" Count="11" />
      <LineId Id="941" Count="0" />
      <LineId Id="405" Count="4" />
      <LineId Id="51" Count="0" />
      <LineId Id="942" Count="0" />
      <LineId Id="709" Count="2" />
      <LineId Id="714" Count="0" />
      <LineId Id="943" Count="0" />
      <LineId Id="716" Count="3" />
      <LineId Id="708" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>