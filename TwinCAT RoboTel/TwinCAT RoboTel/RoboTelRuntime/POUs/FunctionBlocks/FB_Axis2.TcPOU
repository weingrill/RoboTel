<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="FB_Axis2" Id="{a3e55664-127b-4a99-a2ba-301ba26f68b3}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_Axis2
VAR_INPUT
	// enable the axis
	Enable:		BOOL;
	// reset the axis after error
	Reset: 		BOOL;
	// Move the Axis to a given position
	MoveAxis: 	BOOL;
	// HomeAxis;
	HomeAxis:	BOOL;
	// Stop the axis
	StopAxis:	BOOL;
	// position to move to
	Position: 	LREAL;
	// maximum velocity
	Velocity:	LREAL := 10.0;
	// Enable movement in positive direction (considering limit switch)
	Enable_Positive:	BOOL := TRUE;
	// Enable movement in negative direction (considering limit switch)
	Enable_Negative:	BOOL := TRUE;
	// jog forward
	Jog_Forward:		BOOL := FALSE;
	// jog backward
	Jog_Backwards:		BOOL := FALSE;
	// calibration input for homing
	bCalibrationCam:	BOOL;
	isModuloAxis:		BOOL := FALSE;
	// enable external setpoint generator
	Tracking:			BOOL;
	HomingMode:			MC_HomingMode := MC_HomingMode.MC_DefaultHoming;
END_VAR
VAR_OUTPUT
	// Reset has been executed
	ResetDone:	BOOL; 
	// the axis is in target position
	MoveDone:	BOOL;
	// the axis hass jogged
	JogDone:	BOOL;
	// the axis has stopped
	StopDone: 	BOOL;
	// the axis was calibrated and has come to a standstill
	HomeDone:	BOOL;
	// the axis is ready
	Ready:		BOOL;
	// the command has been aborted
	CommandAborted: BOOL;
	// axis busy
	Busy: 		BOOL;
	// axis is moving
	Active: 	BOOL;
	// axis is stopping
	Stopping:	BOOL;
	// axis is standing still (no commands issued) this should be the default
	StandStill:	BOOL;
	// axis is in motion
	InMotion:	BOOL;
	// axis is homing
	Homing:		BOOL;
	// axis is Accelerating or Decelerating
	InRamp:		BOOL;
	// axis is tracking on position
	isTracking:	BOOL;
	//	error condition
	Error: 		BOOL;
	// number of error
	ErrorID: 	UDINT;
	// actual position of the axis
	ActualPosition: LREAL;
	// axis is calibrated;
	Calibrated:	BOOL := FALSE;
	
END_VAR
VAR
	Axis_Power:		MC_Power;
	Axis_Reset:		MC_Reset;
	Axis_Move:		MC_MoveAbsolute;
	Axis_Modulo:	MC_MoveModulo;
	//Axis_Position:	MC_ReadActualPosition;
	Axis_Stop:		MC_Stop;
	Axis_Status:	MC_ReadStatus;
	Axis_Home:		MC_Home;
	Axis_Jog: 		MC_Jog;
	Axis_SetpointEnable:	MC_ExtSetPointGenEnable;
	Axis_SetpointDisable:	MC_ExtSetPointGenDisable;
	StopAllowed:	BOOL;
	// Direction for Axis_SetpointFeed
	Direction:			DINT;
	// difference between set position and measured position
	PositionDifference:	LREAL;
	TrackPosition:		LREAL;
	TrackVelocity:		LREAL;
	TrackAcceleration:	LREAL;
END_VAR

VAR_IN_OUT
	// axis reference
	AxisRef:	AXIS_REF;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[// query the status of the axis at the beginning of the cycle
Axis_Status(Enable := TRUE,
			Axis := AxisRef);

// Enable the axis (power signal or manual enable)
Axis_Power(
	Axis := AxisRef,
	Enable := Enable,
	Enable_Positive := Enable_Positive,
	Enable_Negative := Enable_Negative,
	Override := 100.0,
	Status => Ready);

Axis_Reset(
	Axis := 	AxisRef,
	Execute := 	Reset,
	Done => 	ResetDone);

(* Tracking section ********************************************** *)

IF Tracking THEN
	PositionDifference := LIMIT(-0.002, Position - ActualPosition, 0.002);
	TrackPosition := ActualPosition + PositionDifference;
	TrackVelocity := 0.0; // LIMIT(-1.0, 2.0 * Velocity, 1.0);
	TrackAcceleration := 0.0;
ELSE
	TrackPosition := ActualPosition;
	TrackVelocity := 0.0;
	TrackAcceleration := 0.0;
END_IF


IF NOT Tracking OR ABS(PositionDifference) < 1E-5 THEN // TrackVelocity = 0 AND TrackAcceleration = 0 THEN
	Direction := 0;
ELSIF PositionDifference < 0.0 THEN
	Direction := -1;
ELSE
	Direction := 1;
END_IF

isTracking := Tracking AND ABS(Position - ActualPosition) < 5.0E-3;
IF Tracking AND Ready AND Calibrated AND NOT StopAxis THEN
	Axis_SetpointEnable.Execute := TRUE;
ELSIF NOT Axis_SetpointEnable.Busy AND Axis_SetpointEnable.Done THEN
	Axis_SetpointEnable.Execute := FALSE;
END_IF

Axis_SetpointEnable(Axis := AxisRef,
	Position :=		TrackPosition,	// final destination
	PositionType := POSITIONTYPE_ABSOLUTE);
	
MC_ExtSetPointGenFeed(
	Position :=		TrackPosition,
	Velocity :=		TrackVelocity,
	Acceleration :=	TrackAcceleration,
	Direction :=	Direction, 
	Axis :=			AxisRef);

Axis_SetpointDisable(Axis := AxisRef,
	Execute := NOT Tracking);
	
(* end of tracking section  ********************************************** *)
	
IF isModuloAxis THEN
	Axis_Modulo( 
		Axis := 		AxisRef,
		Execute := 		MoveAxis AND Ready AND Calibrated,
		Position := 	Position,
		Velocity :=		Velocity,
		Direction := 	MC_Shortest_Way,
		Done => 		MoveDone);
ELSE
	Axis_Move( 
		Axis := 		AxisRef,
		Execute := 		MoveAxis AND Ready AND Calibrated,
		Position := 	Position,
		Velocity := 	Velocity,
		Done => 		MoveDone);
END_IF

Axis_Jog(
	JogForward := 	Jog_Forward AND Ready AND Enable_Positive,
	JogBackwards := Jog_Backwards AND Ready AND Enable_Negative,
	Mode := 		MC_JOGMODE_STANDARD_FAST,
	Done => 		JogDone, 
	Axis := 		AxisRef);

Axis_Home(
	Axis := 			AxisRef,
	Execute := 			HomeAxis AND Ready,
	Position := 		position, //DEFAULT_HOME_POSITION,
	HomingMode :=		HomingMode,
	bCalibrationCam := 	bCalibrationCam,
	Done => 			HomeDone);	
(*	
IF (MoveDone OR HomeDone OR JogDone) AND NOT Axis_Status.StandStill THEN
	StopAxis := TRUE;
END_IF
*)
IF MoveDone AND (NOT Axis_Modulo.Busy OR NOT Axis_Move.Busy) THEN
	MoveAxis := FALSE;
END_IF

IF JogDone THEN
	Jog_Forward := FALSE;
	Jog_Backwards := FALSE;
END_IF

IF HomeDone THEN
	Calibrated := TRUE;
	HomeAxis := FALSE;
END_IF

IF StopDone THEN
	StopAxis := FALSE;
END_IF

StopAllowed :=	Axis_Status.StandStill OR  
				Axis_Status.ContinuousMotion OR
				Axis_Status.DiscreteMotion OR
				Axis_Status.SynchronizedMotion OR
				Axis_Status.Homing;

Axis_Stop( 
	Axis := 	AxisRef,
	Execute := 	StopAxis AND StopAllowed,
	Done => 	StopDone);
(*
Axis_Position(
	Axis := 	AxisRef,
	Enable := 	Calibrated,
	Position => AbsolutePosition);
*)
IF isModuloAxis THEN
	ActualPosition := AxisRef.NcToPlc.ModuloActPos; // required e.g. for Filterwheel
ELSE
	ActualPosition := AxisRef.NcToPlc.ActPos;
END_IF

IF Axis_Power.Error THEN
	ErrorID := Axis_Power.ErrorID;
END_IF

IF Axis_SetpointEnable.Error THEN
	ErrorID := Axis_SetpointEnable.ErrorID;
END_IF

IF Axis_Reset.Error THEN
	ErrorID := Axis_Reset.ErrorID;
END_IF

IF Axis_Modulo.Error THEN
	ErrorID := Axis_Modulo.ErrorID;
END_IF

IF Axis_Move.Error THEN
	ErrorID := Axis_Move.ErrorID;
END_IF

IF Axis_Home.Error THEN
	ErrorID := Axis_Home.ErrorID;
END_IF

IF Axis_Jog.Error THEN
	ErrorID := Axis_Jog.ErrorID;
END_IF

IF Axis_Stop.Error THEN
	ErrorID := Axis_Stop.ErrorID;
END_IF

Busy := 	Axis_SetpointEnable.Busy OR
			Axis_Reset.Busy OR 
			Axis_Modulo.Busy OR
			Axis_Move.Busy OR 
			Axis_Home.Busy OR
			Axis_Jog.Busy;

Error := 	Axis_Power.Error OR 
			Axis_SetpointEnable.Error OR
			Axis_Reset.Error OR 
			Axis_Modulo.Error OR
			Axis_Move.Error OR 
			Axis_Home.Error OR
			Axis_Jog.Error OR 
			Axis_Stop.Error;

Active := 	// Axis_Power.Active OR //alsways true if enabled 
			Axis_Modulo.Active OR
			Axis_Move.Active OR 
			Axis_Home.Active OR
			Axis_Jog.Active OR 
			Axis_Stop.Active;

CommandAborted :=	Axis_Modulo.CommandAborted OR
					Axis_Move.CommandAborted OR 
					Axis_Home.CommandAborted OR
					Axis_Jog.CommandAborted OR 
					Axis_Stop.CommandAborted;	
	
IF NOT Error THEN
	ErrorID := 0;
END_IF
			
InMotion := Axis_Status.ConstantVelocity OR
			Axis_Status.DiscreteMotion OR
			Axis_Status.ContinuousMotion OR
			Axis_Status.SynchronizedMotion; 
InRamp :=	Axis_Status.Accelerating OR
			Axis_Status.Decelerating;
Stopping := Axis_Status.Stopping;
StandStill := Axis_Status.StandStill;
Homing :=	Axis_Status.Homing;
// InPosition := AxisRef.NcToPlc.TargetPos;]]></ST>
    </Implementation>
    <LineIds Name="FB_Axis2">
      <LineId Id="2004" Count="212" />
      <LineId Id="386" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>