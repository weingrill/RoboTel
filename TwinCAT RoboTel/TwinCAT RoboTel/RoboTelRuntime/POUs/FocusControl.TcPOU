<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.6">
  <POU Name="FocusControl" Id="{d08d0458-4211-4cc5-9e25-2b5c152fb2d4}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM FocusControl
VAR_INPUT
	// enable the axis
	enable: BOOL;
	// Move M2 closer to M1
	inward: 	BOOL;
	// Move M2 away from M1
	outward: 	BOOL;	
	// reset the axis(-error)
	reset: 		BOOL;
	// move the axis to new position
	MoveAxis:	BOOL;
	// calibrate the focus axis
	HomeAxis:	BOOL;
	// focus position in millimeters
	position: 		LREAL;
END_VAR

VAR_OUTPUT
	Error: 		BOOL;
	ErrorID:	UDINT;
END_VAR

VAR
	FocusUnlock:		RS;
	FocusDelay:			TON;
	FocusAxis:			FB_Axis2;
	actual_position:	LREAL;
	FocusUnlocked:		BOOL;
	FocusAxisEvent:		FB_EventLog;
END_VAR

VAR CONSTANT
	Focus_precision:	LREAL := 0.001;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[// Focus is locked by default
//GVL_Telescope.focus_lock := FALSE;

// unlock the Focus 
// TODO: onlock the Focus on movement only
FocusUnlock(SET := enable,
			RESET1 := reset OR NOT enable,
			Q1 => GVL_Telescope.focus_lock);

// wait until the focus is unlocked for n milliseconds
FocusDelay(IN :=FocusUnlock.Q1, PT := T#1000MS, Q => FocusUnlocked);

//GVL_Telescope.focus_lock := FocusUnlocked;
FocusAxis(	
	Enable := 			FocusUnlocked,
	Reset := 			reset,
	MoveAxis := 		MoveAxis,
	HomeAxis :=			HomeAxis,
	Position := 		position,
	Velocity :=			5.0,
	Enable_Positive := 	NOT GVL_Telescope.focus_limit_far,
	Enable_Negative := 	NOT GVL_Telescope.focus_limit_near,
	Jog_Forward :=		outward,
	Jog_Backwards := 	inward,
	bCalibrationCam :=  GVL_Telescope.focus_limit_near,
	ActualPosition => 	actual_position,
	Error =>			error,
	ErrorID =>			ErrorID,
	AxisRef :=			GVL_Telescope.FocusAxisRef);	

IF FocusAxis.MoveDone THEN
	MoveAxis := FALSE;
END_IF

IF FocusAxis.HomeDone THEN
	HomeAxis := FALSE;
END_IF
	
IF FocusAxis.ResetDone THEN
	reset := FALSE;
END_IF

FocusAxisEvent(	
	Trigger := 		FocusAxis.Error, 
	Level := 		ADSLOG_MSGTYPE_ERROR,
	FormatString :=	'Focus Axis Error: %s',
	OnMessage := 	DWORD_TO_HEXSTR(FocusAxis.ErrorID, 4, FALSE),
	OffMEssage := 	'OK',
	OffLevel := 	ADSLOG_MSGTYPE_HINT);
]]></ST>
    </Implementation>
    <LineIds Name="FocusControl">
      <LineId Id="115" Count="0" />
      <LineId Id="54" Count="0" />
      <LineId Id="310" Count="0" />
      <LineId Id="87" Count="0" />
      <LineId Id="236" Count="0" />
      <LineId Id="99" Count="2" />
      <LineId Id="274" Count="0" />
      <LineId Id="112" Count="0" />
      <LineId Id="110" Count="1" />
      <LineId Id="237" Count="0" />
      <LineId Id="90" Count="0" />
      <LineId Id="176" Count="0" />
      <LineId Id="91" Count="1" />
      <LineId Id="175" Count="0" />
      <LineId Id="93" Count="0" />
      <LineId Id="239" Count="0" />
      <LineId Id="94" Count="1" />
      <LineId Id="106" Count="1" />
      <LineId Id="177" Count="1" />
      <LineId Id="97" Count="0" />
      <LineId Id="114" Count="0" />
      <LineId Id="98" Count="0" />
      <LineId Id="179" Count="11" />
      <LineId Id="192" Count="6" />
      <LineId Id="156" Count="1" />
    </LineIds>
  </POU>
</TcPlcObject>