<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.6">
  <POU Name="FilterWheelControl" Id="{f0e4f375-0934-4386-a9fa-94537c34c7e1}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM FilterWheelControl
VAR_INPUT
	// enable the axis
	Enable:				BOOL;
	// reset the axis(-error)
	Reset:				BOOL;
	// move counter-clockwise
	left:				BOOL;
	// move clockwise
	right:				BOOL;
	// TRUE, if the filter is in position and wheel has come to rest
	Ready:				BOOL;
	// filter position
	filter_position: 	WORD;
END_VAR
VAR_OUTPUT
	// is true if an error occurred
	error: 				BOOL;
	// number of current axis error
	ErrorID:			UDINT;
	act_position:		LREAL;
END_VAR
VAR
	// move to given position
	MoveAxis: 			BOOL;
	// calibrate the axis
	HomeAxis:			BOOL;
	FilterWheelAxis:	FB_Axis2;
	// axis position to set to with `MoveAxis`
	position:			LREAL;
	// current filter determined from the axis position
	actual_filter:		WORD;
	FilterWheelEvent:	FB_EventLog;
END_VAR
VAR CONSTANT
	// total number of filters in the filter wheel
	filters:			WORD := 24;
	// current position of the calibration cam. Apply an offset here if necessary
	calibration_position:	LREAL := 0.0;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* This program controls the filter wheel. 
The current optical filter is selected through `filter_position`.
The number of filters is hard coded to 24.
Use `MoveAxis` to position the 
*)

// limit filter_position to [0...filters-1]
filter_position := filter_position MOD filters;
// calculate the actual filter position
actual_filter := LREAL_TO_WORD(act_position * filters / 360.0);

Ready := (filter_position = actual_filter) AND FilterWheelAxis.StandStill;

IF FilterWheelAxis.Calibrated THEN
	HomeAxis := FALSE;
	// if we are on automatic mode then position the filterwheel
	IF TelescopeControl.power OR PendantControl.enable THEN
		position := 360.0 * WORD_TO_LREAL(filter_position) / WORD_TO_LREAL(filters);
		MoveAxis := filter_position <> actual_filter;
	END_IF
ELSE
	position := calibration_position;
	HomeAxis := TRUE;
END_IF

FilterWheelAxis(
	Enable := 			Enable,
	Reset := 			Reset,
	MoveAxis :=			MoveAxis,
	HomeAxis :=			HomeAxis,
	Position := 		position, 
	Enable_Positive :=  TRUE,
	Enable_Negative := 	TRUE,
	Jog_Forward :=		left,
	Jog_Backwards := 	right,
	bCalibrationCam :=	NOT GVL_FilterWheel.bCalibrationCam,
	isModuloAxis :=		TRUE,
	ActualPosition => 	act_position,
	Error => 			error,
	ErrorID => 			ErrorID,
	AxisRef :=			GVL_FilterWheel.FilterWheel_AxisRef);

IF FilterWheelAxis.MoveDone THEN
	MoveAxis := FALSE;
END_IF

IF FilterWheelAxis.HomeDone THEN
	HomeAxis := FALSE;
END_IF
	
IF FilterWheelAxis.ResetDone THEN
	reset := FALSE;
END_IF

FilterWheelEvent(	
	Trigger := 		Error, 
	Level := 		ADSLOG_MSGTYPE_ERROR,
	FormatString :=	'Filter Wheel Axis Error: %s',
	OnMessage := 	NCError_TO_STRING(ErrorID),
	OffMEssage := 	'OK',
	OffLevel := 	ADSLOG_MSGTYPE_HINT);]]></ST>
    </Implementation>
    <LineIds Name="FilterWheelControl">
      <LineId Id="184" Count="0" />
      <LineId Id="219" Count="0" />
      <LineId Id="218" Count="0" />
      <LineId Id="220" Count="0" />
      <LineId Id="217" Count="0" />
      <LineId Id="334" Count="0" />
      <LineId Id="215" Count="1" />
      <LineId Id="224" Count="0" />
      <LineId Id="222" Count="0" />
      <LineId Id="235" Count="1" />
      <LineId Id="87" Count="1" />
      <LineId Id="177" Count="0" />
      <LineId Id="254" Count="0" />
      <LineId Id="249" Count="0" />
      <LineId Id="252" Count="1" />
      <LineId Id="251" Count="0" />
      <LineId Id="91" Count="0" />
      <LineId Id="179" Count="1" />
      <LineId Id="90" Count="0" />
      <LineId Id="181" Count="0" />
      <LineId Id="45" Count="0" />
      <LineId Id="58" Count="0" />
      <LineId Id="46" Count="1" />
      <LineId Id="99" Count="0" />
      <LineId Id="48" Count="4" />
      <LineId Id="96" Count="1" />
      <LineId Id="53" Count="2" />
      <LineId Id="28" Count="0" />
      <LineId Id="128" Count="0" />
      <LineId Id="122" Count="0" />
      <LineId Id="130" Count="1" />
      <LineId Id="133" Count="0" />
      <LineId Id="132" Count="0" />
      <LineId Id="134" Count="1" />
      <LineId Id="129" Count="0" />
      <LineId Id="137" Count="2" />
      <LineId Id="136" Count="0" />
      <LineId Id="123" Count="4" />
      <LineId Id="5" Count="0" />
      <LineId Id="147" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>